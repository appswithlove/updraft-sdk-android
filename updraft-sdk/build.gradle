plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'base'
    id 'com.appswithlove.loco'
}

def packageName = 'com.appswithlove.updraft'

android {

    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 31

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId packageName
            version = libraryVersion
            artifactId artifact

            // Tell maven to prepare the generated "*.aar" file for publishing
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.api.allDependencies.each { dependency ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api 'androidx.appcompat:appcompat:1.0.0'

    api 'com.squareup.retrofit2:retrofit:2.4.0'
    api 'com.squareup.retrofit2:converter-gson:2.4.0'
    api 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    api 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    api "io.reactivex.rxjava2:rxjava:2.1.12"
    api 'io.reactivex.rxjava2:rxandroid:2.0.2'

    api 'com.simplify:ink:1.0.0'

    api 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    api 'androidx.lifecycle:lifecycle-runtime:2.0.0'
    annotationProcessor 'androidx.lifecycle:lifecycle-compiler:2.0.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'

    api 'com.rm:freedrawview:1.1.2'
    implementation "androidx.core:core-ktx:+"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.0.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

group = publishedGroupId
version = libraryVersion


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    options.encoding = 'UTF-8'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

afterEvaluate {
    assembleRelease.dependsOn(clean)
    tasks.withType(GenerateMavenPom).all {
        it.dependsOn assembleRelease
    }
}

Loco {
    apiKey = 'sOLd6p87EQ9zFoOeI8ytGBvGhKNIFo4f'
    lang = ['en', 'de']
    defLang = 'en'
    resDir = "$projectDir/src/main/res"
    fallbackLang = 'en'
    orderByAssetId = true
    hideComments = true
}
